10 F$="PRGFLASH.BIN":GOSUB 3000:CLEAR 200,SA-1:CLS
15 BA=&H64:GOSUB 4000:'SET COCOFLASH BASE ADDRESS
90 LOADM"PRGFLASH.BIN":EXEC:'SETS UP ALL RAM MODE AND DEFINES USR    FUNCTIONS
100 LINE INPUT "BIN FILE NAME:";F$:IF INSTR(F$,".")=0 THEN F$=F$+".BIN"
110 GOSUB 3000:IF SA<>&H4000 THEN PRINT "IMPROPER LOAD ADDRESS! FILE MUSTLOAD AT &H4000!":GOTO 100 ELSE LOADM F$
120 POKE &HFF7F,&H30:POKE &HFF00+BA,0:POKE &HFF7F,0:'SELECT SLOT 1
125 PRINT "SIZE=";BL;"BYTES":IF BL/1024=INT(BL/1024) THEN PRINT "THIS IS A";BL/1024;"K ROM":PRINT "SET SIZE TO";INT((BL-1)/4096+1)*4;"K"
130 INPUT "SIZE IN K TO PROGRAM:";K
140 A=0:FAB=32767
150 B=2:PRINT "SEARCHING FOR AN EMPTY";K;"K":PRINT "SPACE...";:'START CHECK AT BANK 2, BANKS 0 AND 1 ARE FOR THE MENU
160 SPIN=ABS(SPIN*(SPIN<4))+1:PRINT CHR$(8);MID$("!/-"+CHR$(92),SPIN,1);:IF B<2048 THEN S=USR6(B):IF S=0 THEN FAB=32767:B=B+1:GOTO 160
165 IF B<FAB THEN FAB=B
170 A=A+4:IF A<K AND B<2047 THEN B=B+1:GOTO 160
180 B=FAB:PRINT CHR$(8);".":IF B<2048 THEN PRINT "SPACE IS AVAILABLE AT BANK";B ELSE PRINT "NO EMPTY SPACE AVAILABLE?!?"
190 INPUT "FLASH ROM BANK TO LOAD:";B:IF B<0 OR B>2047 THEN 190
200 GOSUB 2000:IF WOK=0 THEN PRINT "BANK IS IN USE!":GOSUB 1070:INPUT E$:IF LEFT$(E$,1)="Y"OR LEFT$(E$,1)="y"THEN FOR X=B TO EB:A=USR3(X):NEXT X ELSE PRINT "YOU CAN NOT WRITE TO A BANK THATIS ALREADY IN USE WITHOUT       ERASING IT FIRST.":END
210 A=USR2(B):IF A<>B THEN PRINT "ERROR!":BREAK
220 IF K>0 THEN K=K-4:PRINT "PROGRAMMING BANK ";A:A=USR0(&H4000+MO):IF A>0 THEN MO=MO+&H1000:GOTO 220 ELSE PRINT "ERROR!":STOP
225 A=USR2(B)
230 PRINT "DONE!"
999 END
1000 FB=B+4
1010 IF FB>2047 THEN FB=FB-2048
1020 IF FB<16 THEN FB=INT(FB/2)*2:LB=FB+1 ELSE FB=INT(FB/16)*16:LB=FB+15
1030 FB=FB-4:LB=LB-4
1040 IF FB<0 THEN FB=FB+2048
1050 IF LB<0 THEN LB=LB+2048
1060 RETURN
1070 PRINT "THESE 4K BANKS ARE IN USE AND   ARE PART OF THE BLOCK THAT WILL BE ERASED:":FOR X=FB TO LB:IF USR6(X)=0 THEN PRINT X,
1080 NEXT X:PRINT:PRINT "ERASE BANKS";FB;"THROUGH";LB;:RETURN
2000 AK=K:EB=B
2010 AK=AK-4:IF AK>0 THEN EB=EB+1:GOTO 2010
2020 WOK=1:FOR X=B TO EB:WOK=WOK*USR6(X):NEXT X
2030 GOSUB 1000:B1=FB:B2=B:B=EB:GOSUB 1000:FB=B1:B=B2:RETURN
3000 OPEN "D",1,F$,5:FIELD #1,5 AS H$:GET #1,1:SA=ASC(MID$(H$,4,1))*256+ASC(MID$(H$,5,1)):BL=ASC(MID$(H$,2,1))*256+ASC(MID$(H$,3,1)):CLOSE #1:RETURN
4000 P=PEEK(&HFF80):POKE &HFF80,&H55:POKE &HFF80,&HAA:POKE &HFF80,&H21:POKE &HFF80,&H01:POKE &HFF80,BA:RETURN

