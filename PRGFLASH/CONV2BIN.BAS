10 F$="CONV2BIN.BIN":GOSUB 3000:CLEAR 200,SA-1:CLS
20 LOADM"CONV2BIN.BIN":EXEC:'   DEFINES USR FUNCTIONS
30 INPUT"ROM FILE TO CONVERT";R$
40 OPEN "D",1,R$,1:FIELD #1,1 AS F$
50 IF LOF(1)>16384 THEN PRINT"UNABLE TO CONVERT FILES LARGER  THAN 16384 BYTES, PLEASE SPLIT  THIS FILE INTO SMALLER PIECES   AND TRY AGAIN.":END
60 PAD=0:L=LOF(1)
70 IF INT((L+PAD)/128)<>(L+PAD)/128 THEN PAD=PAD+1:GOTO 70
80 IF PAD>0 THEN PRINT "PADDING FILE WITH";PAD;"BYTES.":L=LOF(1):LSET F$=CHR$(255):FOR X=1 TO PAD:PUT #1,L+X:NEXT X
90 NLF=LOF(1):PRINT "FILE LENGTH IS NOW";NLF;"BYTES."
100 CLOSE #1
110 OPEN "D",1,R$,128:FIELD #1,128 AS F$:PRINT "COPYING FILE DATA INTO RAM AT   ADDRESS &H4000."
120 FOR R=1 TO LOF(1):ADD$=HEX$(&H4000+(R-1)*128):T=USR0(ADD$)
130 GET #1,R:B=USR1(F$)
140 NEXT R:CLOSE #1
150 P=INSTR(R$,"."):IF P<=0 THEN R$=R$+".BIN" ELSE IF P=1 OR MID$(R$,P+1)="BIN" THEN R$="TEMP.BIN" ELSE R$=LEFT$(R$,P-1)+".BIN"
160 PRINT "CREATING ";R$
170 SAVEM R$,&H4000,&H4000+NLF-1,&H4000
180 PRINT "DONE."
2999 END
3000 OPEN "D",1,F$,5:FIELD #1,5 AS H$:GET #1,1:SA=ASC(MID$(H$,4,1))*256+ASC(MID$(H$,5,1)):BL=ASC(MID$(H$,2,1))*256+ASC(MID$(H$,3,1)):CLOSE #1:RETURN

